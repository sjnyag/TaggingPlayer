apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'

final stampVersionCode = hasProperty('version_code') ? property('version_code').toInteger() : 1
final stampVersionName = hasProperty('version_name') ? '0.1.0.' + property('version_name') : '0.1.0.0'

final stampKeystorePassword = hasProperty('keystore_password') ? property('keystore_password') : ''
final stampKeyAlias = hasProperty('key_alias') ? property('key_alias') : ''
final stampKeyPassword = hasProperty('key_password') ? property('key_password') : ''
final stampKeyFile = hasProperty('key_file') ? property('key_file') : 'ci/debug.jks'
final stampPlayAPIJson = hasProperty('play_api_json') ? property('play_api_json') : 'ci/play.json'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.sjn.stamp"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode stampVersionCode
        versionName stampVersionName
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        abortOnError true
    }
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/MANIFEST.MF'
    }
    signingConfigs {
        src {
            storeFile file(stampKeyFile)
            storePassword stampKeystorePassword
            keyAlias stampKeyAlias
            keyPassword stampKeyPassword
        }
    }
    buildTypes {
        debug {
            debuggable true
        }
        release {
            debuggable false
            minifyEnabled false
            signingConfig signingConfigs.src
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all {
        variant -> variant.resValue "string", "versionName", variant.versionName
    }
    sourceSets { main { assets.srcDirs = ['src/main/assets', 'src/main/assets/'] } }
    dataBinding {
        enabled = true
    }
    kapt {
        generateStubs = true
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven { url "https://jitpack.io" }
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android Support Libraries
    implementation "com.android.support:appcompat-v7:$versions.supportLib"
    implementation "com.android.support:cardview-v7:$versions.supportLib"
    implementation "com.android.support:leanback-v17:$versions.supportLib"
    implementation "com.android.support:design:$versions.supportLib"
    implementation "com.android.support:mediarouter-v7:$versions.supportLib"
    implementation "com.android.support.constraint:constraint-layout:1.1.1"
    implementation 'com.android.support:multidex:1.0.3'

    // google/ExoPlayer
    implementation project(':exoplayer-library-core')
    implementation project(':exoplayer-library-dash')
    implementation project(':exoplayer-library-hls')
    implementation project(':exoplayer-extension-flac')

    // Firebase
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }
    implementation 'com.google.android.gms:play-services-cast-framework:15.0.1'
    implementation "com.google.firebase:firebase-analytics:$versions.firebase"
    implementation "com.google.firebase:firebase-core:$versions.firebase"
    implementation "com.google.firebase:firebase-crash:$versions.firebase"

    implementation 'com.google.guava:guava:20.0'
    implementation 'com.gordonwong:material-sheet-fab:1.2.1'
    implementation 'com.github.sjnyag:AnimationWrapLayout:0.3.0'
    implementation 'com.github.sjnyag:Colorful:hotfix-SNAPSHOT'
    implementation 'com.github.sjnyag:ForceAnimateAppBarLayout:0.4.0'
    implementation 'com.github.takusemba:spotlight:1.1.1'
    implementation 'com.github.vajro:MaterialDesignLibrary:1.6'
    implementation 'com.jakewharton:disklrucache:2.0.2'
    implementation('com.mikepenz:materialdrawer:5.3.0@aar') {
        transitive = true
    }
    implementation 'com.nanohttpd:nanohttpd-webserver:2.1.1'
    implementation 'com.qhutch.elevationimageview:elevationimageview:2.0'
    implementation 'com.sothree.slidinguppanel:library:3.3.0'
    implementation "com.takisoft.fix:preference-v7:$versions.supportLib.0"
    implementation 'eu.davidea:flexible-adapter:5.0.0-rc3'
    implementation 'net.danlew:android.joda:2.6.0'
    implementation 'net.yslibrary.licenseadapter:licenseadapter:1.4.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
    kapt "com.android.databinding:compiler:$versions.android_plugin"

    // Realm
    kapt 'io.realm:realm-annotations:3.5.0'
    kapt 'io.realm:realm-annotations-processor:3.5.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.10.0'
    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation "com.android.support:support-annotations:$versions.supportLib"
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}

play {
    track = 'alpha'
    jsonFile = file(stampPlayAPIJson)
}
apply plugin: 'com.google.gms.google-services'
